
ods escapechar="^";


proc format;
 
  value pcolor 0.1 - high = "Red"
               0.05 -< 0.1 = "Yellow"
			   low -< 0.05 = "Green"
			   ;
  value effcolor -0.5 - 0.5 = "Red"
                 0.5 <- 0.8 = "Orange"
				 -0.8 -< -0.5 = "Orange"
				 0.8 <- 1.5 = "Yellow"
				 -1.5 -< -0.8 = "Yellow"
				 1.5 <- 2 = "Lightgreen"
				 -2 -< -1.5 = "Lightgreen"
				 2 <- high = "Green"
				 low -< -2 = "Green"
                 ;
run;


%macro report_mixedANOVA(ds=, title=);

ods rtf anchor="&ds";

ods rtf text="^{newline}";
ods text="^{style[fontsize=12pt color=blue]1. Descriptive Statistics of Raw Value}";
ods proclabel="Descriptive statistics for raw value";
proc report data=des_&ds contents="" missing split="^";
  format trtn trtn.;
  columns count trtn n missing mean std median q1 q3 min max;
  define count / group noprint;
  define trtn / group "Treatment" order=internal style(column)=[just=left];
  define n / display style(column)=[just=center];
  define missing / display style(column)=[just=center];
  define mean / display style(column)=[just=center];
  define std / display style(column)=[just=center];
  define median / display style(column)=[just=center];
  define q1 / display style(column)=[just=center];
  define q3 / display style(column)=[just=center];
  define min / display style(column)=[just=center];
  define max / display style(column)=[just=center];
  compute after trtn;
    line "";
	endcomp;
  break before count / contents="" page;
title "Descriptive statistics for &title";
run;
title;

ods rtf text="^{newline}";
ods text="^{style[fontsize=12pt color=blue]2. Box Plot of Raw Value}";
ods proclabel="Box plot for raw value";
proc sgplot data=&ds dattrmap=attrmap description="";
  format trtn trtn.;
  vbox evstresn / group=trtn medianattrs=(pattern=2 color=black) meanattrs=(symbol=circlefilled color=black size=6)
                  boxwidth=0.9 whiskerattrs=(pattern=2) attrid=myid1;
  xaxis label=" " valueattrs=(size=12);
  yaxis label="Value" labelattrs=(size=12) valueattrs=(size=12) offsetmin=0.1 offsetmax=0.1;
  keylegend / title="" valueattrs=(size=12) noborder ;
  title "Box plot for &title";
run;
title;



ods rtf text="^{newline}";
ods text="^{style[fontsize=12pt color=blue]3. Comparison between Treatments}";
ods proclabel="Comparison between treatments";
proc report data=diffs_trt_&ds missing split="^";
  format trtn trtn. probt pvalue7.4 FalseDiscoveryRate pvalue7.4 eff 7.3;
  columns comp est ci probt FalseDiscoveryRate eff;
  define comp / group "Comparison" style(column)=[just=left] order=data;
  define est / display "Estimate (StdErr)" style(column)=[just=center];
  define ci / display "95% CI" style(column)=[just=center];
  define probt / display "P-Value" style={background=pcolor. just=center};
  define FalseDiscoveryRate / display "Adjusted P*" style={background=pcolor. just=center};
  define eff / display "Effect Size" style={background=effcolor. just=center};
  compute after comp;
    line "";
	endcomp;
title1 "Comparison of change/D0 between treatments at different time point";
title2 "&title";
footnote j=l "*: adjusted with BH";
run;
title;
footnote;

ods rtf text="^{newline}";
ods text="^{style[fontsize=12pt color=blue]4. Type 3 Tests of Fixed Effect}";
ods proclabel="Type 3 tests";
proc report data=model_&ds missing split="^";
  format probf pvalue7.4;
  column eff probf;
  define eff / display "Effect" style(column)=[just=left];
  define probf / display "P-Value" style={background=pcolor. just=center};
title1 "Type 3 tests of fixed effects";
title2 "&title";
run;
title;

proc sort data=lsm_&ds; by trtn; run;


ods rtf text="^{newline}";
ods text="^{style[fontsize=12pt color=blue]5. Bar Graph of Estimated Value}";
ods proclabel="Bar graph for estimated change/D0";
proc sgplot data=lsm_&ds dattrmap=attrmap;
  format trtn trtn.;
  vbarbasic trtn / group=trtn response=estimate attrid=myid1 name="eff";
  scatter x=trtn y=estimate / yerrorupper=upper yerrorlower=lower markerattrs=(size=0)
                                 errorbarattrs=(pattern=2 color=black)
								 attrid=myid1;
  keylegend "eff" / title=" " noborder valueattrs=(size=12);
  xaxis type=discrete discreteorder=unformatted label=" " valueattrs=(size=12);
  yaxis label="Estimated value" labelattrs=(size=12) valueattrs=(size=12) offsetmin=0.1 offsetmax=0.1;
title1 "Bar graph for estimated value";
title2 "&title";
run;
title;

%mend;
