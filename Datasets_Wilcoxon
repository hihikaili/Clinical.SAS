/***** create datasets *****/
%macro datasets_wilcoxon(ds=, evtest=, site=, grp=, order=);
data &ds;
  set adeff;
  by test evtest trtn trt randoid visitdy;
  where evtest="&evtest" and site=&site;
run;

/*** descriptive ***/
proc means data=&ds n nmiss mean std median q1 q3 min max clm;
  format trtn trtn. visitdy timenum.;
  by trtn;
  class visitdy;
  var evstresn;
  output out=des_&ds n=n_ nmiss=nmiss_ mean=mean_ std=std_ median=median_ q1=q1_ q3=q3_ min=min_ max=max_ lclm=lclm_ uclm=uclm_;
run;

data des_&ds;
  set des_&ds;
  where _type_ ne 0;
  N=put(n_, 3.0);
  Missing=put(nmiss_, 3.0);
  Mean=put(mean_, 5.2);
  Std=put(std_, 6.3);
  Median=put(median_, 5.2);
  Q1=put(q1_, 5.2);
  Q3=put(q3_, 5.2);
  Min=put(min_, 5.2);
  Max=put(max_, 5.2);
  count=1;
run;

proc freq data=&ds;
  format trtn trtn. visitdy timenum.;
  tables trtn*visitdy*evstresc / out=freq_&ds outpct;
run;

data freq_&ds;
  set freq_&ds;
  value=strip(put(count, 3.0))||" ("||strip(put(pct_row, 5.2))||"%)";
run;

proc transpose data=freq_&ds out=freq1_&ds(drop=_name_);
  format trtn trtn. visitdy timenum.;
  by trtn visitdy;
  id evstresc;
  var value;
run;

data freq2_&ds;
  set freq1_&ds;
  n=0;
  array temp{*} _:;
  do i = 1 to dim(temp);
    if temp[i]="" then temp[i]="0 (0.00%)";
	n=input(scan(temp[i], 1, "("), 3.0)+n;
	end;
run;

/*** inferential ***/
proc npar1way data=&ds wilcoxon;
  where treatment in ("Group 1", "Group 2");
  class trt;
  var evstresn;
ods output WilcoxonTest=wilcoxon12_&ds;
run;
ods output close;

data wilcoxon12_&ds;
  set wilcoxon12_&ds;
  evtest="&evtest";
  comp="Neutrogena Regenerating Cream vs. Placebo";
  if name1="P2_WIL";
  pvalue=input(cValue1, 6.4);
  keep evtest comp pvalue;
run;

proc npar1way data=&ds wilcoxon;
  where treatment in ("Group 1", "Group 3");
  class trt;
  var evstresn;
ods output WilcoxonTest=wilcoxon13_&ds;
run;
ods output close;

data wilcoxon13_&ds;
  set wilcoxon13_&ds;
  evtest="&evtest";
  comp='0.15%SM 5253 vs. Placebo';
  if name1="P2_WIL";
  pvalue=input(cValue1, 6.4);
  keep evtest comp pvalue;
run;

proc npar1way data=&ds wilcoxon;
  where treatment in ("Group 2", "Group 3");
  class trt;
  var evstresn;
ods output WilcoxonTest=wilcoxon23_&ds;
run;
ods output close;

data wilcoxon23_&ds;
  set wilcoxon23_&ds;
  evtest="&evtest";
  comp='0.15%SM 5253 vs. Neutrogena Regenerating Cream';
  if name1="P2_WIL";
  pvalue=input(cValue1, 6.4);
  keep evtest comp pvalue;
run;

proc sql;
  create table wilcoxon_&ds as
  select * from wilcoxon12_&ds union
  select * from wilcoxon13_&ds union
  select * from wilcoxon23_&ds
  ;
quit;

%mend;
